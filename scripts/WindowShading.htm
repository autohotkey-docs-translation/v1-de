<!DOCTYPE HTML>
<html>
<head>
<title>Window Shading (ein Fenster bis zur Titelleiste zusammenrollen) -- von Rajat</title>
<meta name="keywords" content="Script">
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<link href="../static/font.css" rel="stylesheet" type="text/css" />
<script src="../static/ga.js" type="text/javascript"></script>
<!--[if lt IE 9]>
<script src="../static/html5.js"></script>
<![endif]-->
<script src="../navbar/NavScript.js" type="text/javascript"></script>
</head>
<body>
<script type="text/javascript">WriteHeader(1);</script>
<div id="content">
  <section>
    <div id="article">
    <h1>Window Shading (ein Fenster bis zur Titelleiste zusammenrollen) -- von Rajat</h1>
    <p>Dieses Script verkleinert ein Fenster auf dessen Titelleiste und beim Dr&uuml;cken eines Hotkeys wieder auf seine urspr&uuml;ngliche Gr&ouml;&szlig;e.  Eine beliebige Anzahl an Fenstern kann auf diese Weise verkleinert werden (das Script merkt sich diese).  Wenn das Script aus irgendeinem Grund beendet wird, dann werden die originalen H&ouml;hen aller "zusammengerollten" Fenstern automatisch wiederhergestellt. </p>
    <p><a href="WindowShading.ahk">Dieses Script herunterladen</a> &nbsp;| &nbsp;<a href="index.htm">Andere Beispiel-Scripts</a> &nbsp;| &nbsp;<a href="../index.htm">Home</a></p>
    <pre class="NoIndent"><em>; H&ouml;he eines zusammengerollten Fensters hier angeben.  Durch das Betriebssystem
; kann die Titelleiste wahrscheinlich gar nicht versteckt werden,
; unabh&auml;ngig davon, wie klein die Zahl ist:</em>
ws_MinHeight = 25

<em>; Diese Zeile wird alle Fenster wieder aufrollen, falls das Script
; aus irgendeinem Grund beendet wird:</em>
OnExit, ExitSub
return  <em>; Ende des automatischen Ausf&uuml;hrungsbereichs</em>

#z::  <em>; &Auml;ndert diese Zeile, um einen anderen Hotkey zu verwenden.
; Danach sollten keine &Auml;nderungen durchgef&uuml;hrt werden, es sei denn,
; die allgemeine Funktionalit&auml;t des Scripts soll ge&auml;ndert werden.
; Hebt die Kommentierung der n&auml;chsten Zeile auf, falls diese Subroutine
; in einen benutzerdefinierten Men&uuml;punkt anstelle eines Hotkeys verwandelt wird.  Die Verz&ouml;gerung erlaubt es,
; das aktive Fenster, welches vom angezeigten Men&uuml; deaktiviert wurde,
; wieder aktiv zu machen:
;Sleep, 200</em>
WinGet, ws_ID, ID, A
Loop, Parse, ws_IDList, |
{
    IfEqual, A_LoopField, %ws_ID%
    {
        <em>; &Uuml;bereinstimmung gefunden, daher sollte das Fenster wiederhergestellt werden (aufrollen):</em>
        StringTrimRight, ws_Height, ws_Window%ws_ID%, 0
        WinMove, ahk_id %ws_ID%,,,,, %ws_Height%
        StringReplace, ws_IDList, ws_IDList, |%ws_ID%
        return
    }
}
WinGetPos,,,, ws_Height, A
ws_Window%ws_ID% = %ws_Height%
WinMove, ahk_id %ws_ID%,,,,, %ws_MinHeight%
ws_IDList = %ws_IDList%|%ws_ID%
return

ExitSub:
Loop, Parse, ws_IDList, |
{
    if A_LoopField =  <em>; Das erste Feld in der Liste ist normalerweise leer.</em>
        continue      <em>; Also &uuml;berspringen.</em>
    StringTrimRight, ws_Height, ws_Window%A_LoopField%, 0
    WinMove, ahk_id %A_LoopField%,,,,, %ws_Height%
}
ExitApp  <em>; Muss f&uuml;r die OnExit-Subroutine durchgef&uuml;hrt werden, um das Script tats&auml;chlich zu beenden.</em>
</pre>
  </div>
  </section>
</div>
<script type="text/javascript">WriteFooter(1);</script>
</body>
</html>